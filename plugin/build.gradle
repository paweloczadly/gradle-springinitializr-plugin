plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'codenarc'
}

group = 'io.oczadly'

repositories {
    mavenCentral()
}

testing {
    suites {
        test {
            useSpock libs.versions.spock.get()
        }

        functionalTest(JvmTestSuite) {
            useSpock libs.versions.spock.get()

            dependencies {
                implementation project()
            }

            targets {
                all {
                    testTask.configure { shouldRunAfter(test) }
                }
            }
        }
    }
}

gradlePlugin {
    plugins {
        springinitializr {
            id = 'io.oczadly.springinitializr'
            implementationClass = 'io.oczadly.SpringInitializrPlugin'
        }
    }
}

gradlePlugin.testSourceSets.add sourceSets.functionalTest

tasks.named('check') {
    dependsOn testing.suites.functionalTest
}

codenarc {
    toolVersion = libs.versions.codenarc.get()
    configFile = file "${rootDir}/config/codenarc/rules.groovy"
}

sourceSets {
    functionalTest {
        resources.srcDir file('src/main/resources')
    }
}
